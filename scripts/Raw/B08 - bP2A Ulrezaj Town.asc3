script_name Brood Wars Protoss 2 - Town A
script_id PB2A

start_campaign
wait 1

start_town
defaultbuild_off
default_min 0
wait 1

#jump to blue support / carrier production if we don't find a nexus
notowns_jump nexus blue_support
unstart_campaign
creep 4
idle_orders disablebuiltin 0 0 0 0 0 0 0
supply 13 Set 250 Max 0 Protoss nriver
build 1 probe 150
build 1 nexus 130
build 8 probe 120
build 1 pylon 80
build 10 probe 120
build 14 probe 80
build 3 gateway 80
build 1 cybernetics_core 80
build 2 stargate 80
build 1 fleet_beacon 80
build 1 citadel_of_adun 80
build 1 templar_archives 80
build 1 robotics_facility 80
build 1 robotics_support_bay 80
build 1 observatory 80
build 18 probe 80
max_workers 18
max_force 550
define_max 20 harchon
define_max 5 observer
define_max 5 shuttle
define_max 8 hmojo
define_max 6 hcorsair
define_max 13 h_artanis
define_max 255 hcarrier
define_max 255 hornet
define_max 5 hzeratul
define_max 3 hdugalle
define_max 54 probe
define_max 2 h_aldaris
define_max 4 hreaver
#hzeratul = u dark templar
#harchon = u zealot
#hmojo = u scout
#hreaver = u reaver
#hcarrier = u carrier
#h_aldaris = u arbiter
#h_artanis = u dragoon
#hcorsair = u corsair
#hdugalle = u dark archon
defensebuild_gg 2 harchon
defenseuse_gg 2 harchon
defensebuild_gg 1 h_artanis
defenseuse_gg 1 h_artanis
defensebuild_gg 1 h_artanis
defenseuse_gg 1 h_artanis
defensebuild_ag 2 h_artanis
defenseuse_ag 2 h_artanis
defensebuild_ag 1 hcorsair
defenseuse_ag 1 hcorsair
defensebuild_ga 1 h_aldaris
defenseuse_ga 1 h_aldaris
#defensebuild_ga 1 carrier
#defenseuse_ga 1 carrier
#does not use hcarriers for def
defensebuild_aa 1 hcorsair
defenseuse_aa 1 hcorsair
idle_orders disablebuiltin 0 0 0 0 0 0 0
idle_orders RechargeShieldsUnit 480 3 Group_Men 1024 shield_battery 30 NotEnemies|Own|Self(Shields(LessThanPercent 25))
idle_orders RechargeShieldsUnit 240 3 Group_Men 1024 shield_battery 40 NotEnemies|Own|Self(Shields(LessThanPercent 25))|Self(Order(Guard))
idle_orders 10 32 1 harchon|hzeratul|hmojo 160 group_men 80 Health(LessThan 14)|WithoutUnitFlags(Air)
idle_orders 10 32 1 hzeratul 192 drone|scv|probe 100 0
idle_orders 10 32 2 harchon 160 drone|scv|probe 100 0
idle_orders 10 32 3 hmojo 192 drone|scv|probe 90 0
idle_orders 10 32 1 harchon|hzeratul|hmojo 160 infested_terran|queen|dark_archon|defiler|lurker|ghost|high_templar|siege_tank 120 Health(LessThan 14)
idle_orders 10 32 1 harchon|hzeratul|hmojo 160 sunken_colony|photon_cannon 90 Health(LessThan 12)
idle_orders 10 32 3 harchon|hzeratul|hmojo 160 group_men 90 Health(LessThan 33)|WithoutUnitFlags(Air)
idle_orders 10 32 3 harchon|hzeratul|hmojo 160 infested_terran|queen|dark_archon|defiler|lurker|ghost|high_templar|siege_tank 130 Health(LessThan 33)
idle_orders 10 32 3 harchon|hzeratul|hmojo 160 sunken_colony|photon_cannon 100 Health(LessThan 24)
idle_orders 10 32 5 harchon|hzeratul|hmojo 160 group_men 95 Health(LessThan 60)|WithoutUnitFlags(Air)
idle_orders 10 32 5 harchon|hzeratul|hmojo 160 infested_terran|queen|dark_archon|defiler|lurker|ghost|high_templar|siege_tank 135 Health(LessThan 60)
idle_orders 10 32 5 harchon|hzeratul|hmojo 160 sunken_colony|photon_cannon|bunker 105 Health(LessThan 45)
idle_orders 10 64 1 h_artanis 192 sunken_colony|photon_cannon|bunker 110 Health(LessThan 20)
idle_orders 10 64 1 h_artanis 192 group_men 100 Health(LessThan 15)
idle_orders 10 64 2 h_artanis 192 group_men 105 Health(LessThan 35)
idle_orders 10 64 4 h_artanis 224 group_men 115 Health(LessThan 60)
idle_orders 10 64 1 h_artanis 192 infested_terran|dark_archon|defiler 130 Health(LessThan 20)
idle_orders 10 64 1 h_artanis 192 lurker|queen 120 Health(LessThan 20)
idle_orders 10 512 8 h_artanis 2048 carrier 250 0
idle_orders 10 32 1 hmojo|hcorsair 192 mutalisk|scout|corsair|scourge|wraith 110 Health(LessThan 20)
idle_orders 10 32 3 hmojo|hcorsair 192 mutalisk|scout|corsair|scourge|wraith 135 Health(LessThan 60)
idle_orders 10 32 1 hmojo|hcorsair 192 devourer|hnorad|battlecruiser|carrier 150 Health(LessThan 20)
idle_orders 10 32 2 hmojo|hcorsair 192 devourer|hnorad|battlecruiser|carrier 160 Health(LessThan 40)
idle_orders 10 32 3 hmojo|hcorsair 192 devourer|hnorad|battlecruiser|carrier 170 Health(LessThan 60)
idle_orders 10 32 4 hmojo|hcorsair 192 devourer|hnorad|battlecruiser|carrier 190 Health(LessThan 80)
idle_orders 10 32 1 hmojo|hcorsair 192 shuttle|observer|overlord 60 Health(LessThan 20)
idle_orders 10 512 8 hmojo|hcorsair 2048 carrier 250 0
idle_orders 59 80 1 hreaver 192 group_men 50 count(AtLeast 2 75 14)|WithoutUnitFlags(Air)
idle_orders 59 80 1 hreaver 192 group_men 80 count(AtLeast 4 75 14)|WithoutUnitFlags(Air)
idle_orders 59 1024 1 hreaver 4096 probe|drone|scv 90 count(AtLeast 2 75 14)|RandomRate(6400 12036)
idle_orders 59 1024 1 hreaver 4096 probe|drone|scv 120 count(AtLeast 4 75 14)|RandomRate(6400 12036)
idle_orders 59 80 2 hreaver 192 group_men 110 count(AtLeast 6 75 14)|WithoutUnitFlags(Air)
idle_orders 10 32 1 hmojo|hcorsair|h_artanis 192 mutalisk|scout|corsair|scourge|devourer|carrier|arbiter 80 Health(LessThan 30)
idle_orders 10 32 2 hmojo|hcorsair|h_artanis 192 mutalisk|scout|corsair|scourge|devourer|carrier|arbiter 90 Health(LessThan 60)
wait_rand 2 3
farms_timing
upgrade 1 p_ground_weapon 40
upgrade 1 p_plasma_shield 40
upgrade 1 observer_speed 40
multirun horror_scarabs
wait 16

idle_orders 184 64 1 hdugalle 612 queen|science_vessel|hnorad|battlecruiser|valhalla|dark_archon 40 Energy(GreaterThanPercent,66)|Self(Energy(GreaterThan 85))
idle_orders 184 64 1 hdugalle 612 defiler|ghost|wraith|phantom|high_templar|corsair 30 Energy(GreaterThanPercent,50)|Self(Energy(GreaterThan 145))
idle_orders 184 0 1 hdugalle 256 group_men 80 Energy(GreaterThan,2)|Deathrattle
train 8 harchon
attack_add 8 harchon
attack_to Loc.25 Loc.24
attack_do
attack_clear
call prepare_carrier_route
upgrade 1 dragoon_range 50
wait 2385

#force target player or else it can get boring
train 3 hmojo
train 1 hzeratul
attack_add 1 hzeratul
attack_add 2 hmojo
attack_to Loc.44 Loc.45
wait 300

attack_do
attack_clear
allies_watch 5 u_town
upgrade 1 p_air_weapon 40
upgrade 1 p_armor 40
upgrade 1 zealot_speed 50
wait 3000

train 2 hcorsair
train 4 h_artanis
train 6 harchon
train 1 hreaver
attack_add 2 h_artanis
attack_add 2 harchon
attack_add 1 hreaver
attack_to Loc.44 Loc.45
wait 300

attack_do
attack_clear
create_script nriver 255 Loc.15 255 0
max_force 833
build 2 robotics_facility 80
tech mind_control 30
upgrade 2 p_ground_weapon 40
place_guard hdugalle 0
wait 2535

call prepare_carrier_route
allies_watch 12 u_town
#idle_orders 182 780 1 hdugalle 912 ultralisk|hnorad|battlecruiser|valhalla|carrier|arbiter 30 health(GreaterThanPercent,66)
idle_orders 182 780 1 hdugalle 912 ultralisk|hnorad|battlecruiser|valhalla|arbiter 30 health(GreaterThanPercent,66)
idle_orders 182 780 1 hdugalle 912 lurker|siege_tank|panzer 20 health(GreaterThanPercent,66)
idle_orders 182 780 1 hdugalle 912 queen|science_vessel|phantom 20 health(GreaterThanPercent,66)
idle_orders 182 780 1 hdugalle 912 defiler|high_templar 10 health(GreaterThanPercent,66)
idle_orders 182 32 1 hdugalle 256 ultralisk|battlecruiser|hnorad 80 incombat|health(GreaterThanPercent,50)
idle_orders 182 32 1 hdugalle 256 lurker|siege_tank|valkyrie|guardian|devourer 30 incombat|health(GreaterThanPercent,80)
idle_orders 182 0 1 hdugalle 256 group_men 80 deathrattle
#carrier excluded because it's used as a special case here
create_script sriver 255 Loc.8 255 0
set_attacks 1
train 2 hcorsair
train 2 hreaver
train 5 h_artanis
attack_add 2 hcorsair
attack_add 2 hreaver
attack_add 5 h_artanis
target_expansion
wait 300

upgrade 1 shuttle_speed 60
upgrade 1 p_plating 40
upgrade 3 p_ground_weapon 40
wait 2540

attack_do
attack_clear
multirun grand_exchange
upgrade 2 dragoon_range 50
train 18 harchon
attack_add 18 harchon
attack_prepare
wait 300

attack_do
attack_clear
defensebuild_gg 1 hzeratul
defenseuse_gg 1 hzeratul
train 4 h_artanis
train 6 harchon
attack_add 7 h_artanis
attack_add 7 harchon
attack_to Loc.54 Loc.55
wait 300

attack_do
attack_clear
call prepare_carrier_route
create_script sriver 255 Loc.14 255 0
tech maelstrom 30
upgrade 1 scarab_damage 40
upgrade 1 scout_sight 40
upgrade 2 p_air_weapon 50
wait 2177

idle_orders 186 882 1 hdugalle 1024 group_men 80 incombat|count(AtLeast 2 60 14)|unitflags(organic)|Self(Energy(GreaterThanPercent 50))
idle_orders 186 0 1 hdugalle 256 group_men 90 unitflags(organic)|deathrattle
set_attacks 1
train 10 h_artanis
train 2 hreaver
train 2 hzeratul
attack_add 2 hzeratul
attack_add 10 h_artanis
attack_add 2 hreaver
target_expansion
wait 300

upgrade 1 scout_speed 40
upgrade 3 p_air_weapon 50
build 1 arbiter_tribunal 80
place_guard hdugalle 0
call prepare_carrier_route
max_force 1150
wait 1515

attack_do
attack_clear
expand 4 u_town
upgrade 2 p_plasma_shield 40
upgrade 2 p_plating 40
upgrade 2 p_armor 40
train 1 h_aldaris
train 7 h_artanis
train 12 harchon
attack_add 1 h_aldaris
attack_add 7 h_artanis
attack_add 12 harchon
attack_prepare
wait 300

attack_do
attack_clear
upgrade 3 p_plating 30
defensebuild_gg 1 hmojo
defenseuse_gg 1 hmojo
defensebuild_ga 1 hmojo
defenseuse_ga 1 hmojo
defensebuild_aa 1 hcorsair
defenseuse_aa 1 hcorsair
train 8 hmojo
attack_add 8 hmojo
attack_to Loc.54 Loc.55
wait 300

attack_do
attack_clear
call prepare_carrier_route
upgrade 3 p_armor 40
upgrade 3 p_plasma_shield 40
:main_loop
wait 1825

set_attacks 1
train 4 hmojo
train 8 h_artanis
train 3 hzeratul
attack_add 3 hzeratul
attack_add 8 h_artanis
attack_add 4 hmojo
target_expansion
wait 1733

attack_do
attack_clear
train 4 hreaver
train 4 hcorsair
train 10 harchon
attack_add 4 hreaver
attack_add 4 hcorsair
attack_add 10 harchon
attack_prepare
wait 300

attack_do
attack_clear
wait 2267

train 1 h_aldaris
train 7 h_artanis
train 2 hdugalle
train 12 harchon
attack_add 1 h_aldaris
attack_add 2 hdugalle
attack_add 7 h_artanis
attack_add 12 harchon
attack_prepare
wait 300

attack_do
expand 5 u_town
goto main_loop

:nriver
start_town
build 1 pylon 80
wait_build 1 pylon
build 1 gateway 80
wait_buildstart 1 gateway
build 2 pylon 80
wait_build 2 pylon
build 1 shield_battery 80
build 1 photon_cannon 80
build 3 pylon 80
wait_build 3 pylon
build 2 photon_cannon 80
:nloop
wait 1500

goto nloop

:sriver
start_town
build 1 pylon 80
wait_build 1 pylon
build 1 gateway 80
wait_buildstart 1 gateway
build 2 pylon 80
wait_build 2 pylon
build 1 shield_battery 80
build 1 photon_cannon 80
build 3 pylon 80
wait_build 3 pylon
build 2 photon_cannon 80
:sloop
wait 1500

goto sloop

:grand_exchange
wait_rand 150 210

call ge_gas
call ge_mins
goto grand_exchange

:ge_gas
resources 13 AtLeast Gas 600 ge_skip
resources 13 AtMost Ore 1399 ge_skip
resources 13 Subtract Ore 150 ge_skip
resources 13 Add Gas 80 ge_skip
return

:ge_mins
resources 13 AtMost Gas 599 ge_skip
resources 13 AtLeast Ore 1300 ge_skip
resources 13 Subtract Gas 100 ge_skip
resources 13 Add Ore 150 ge_skip
return

:ge_skip
return

:prepare_carrier_route
#available guard locations;
# 11, 12, 13, 14, 36, 37 - north after bridge
# 31, 32, 33 - low area south of main
# 34, 35, 38, 39, 40, 41 - canyons
# 42, 43, 10 - misc forts
random_jump 85 prep_cr_1
random_jump 128 prep_cr_2
guard h_artanis Loc.34 1 1 80
guard h_artanis Loc.35 1 1 80
guard h_artanis Loc.11 1 1 80
guard h_artanis Loc.12 1 1 80
guard h_artanis Loc.42 1 1 80
guard hcorsair Loc.40 1 1 80
guard hcorsair Loc.31 1 1 80
guard hcorsair Loc.37 1 1 80
return

:prep_cr_1
guard h_artanis Loc.38 1 1 80
guard h_artanis Loc.41 1 1 80
guard h_artanis Loc.36 1 1 80
guard h_artanis Loc.37 1 1 80
guard h_artanis Loc.10 1 1 80
guard hcorsair Loc.34 1 1 80
guard hcorsair Loc.35 1 1 80
guard hcorsair Loc.43 1 1 80
return

:prep_cr_2
guard h_artanis Loc.14 1 1 80
guard h_artanis Loc.35 1 1 80
guard h_artanis Loc.32 1 1 80
guard h_artanis Loc.33 1 1 80
guard h_artanis Loc.43 1 1 80
guard hcorsair Loc.13 1 1 80
guard hcorsair Loc.14 1 1 80
guard hcorsair Loc.33 1 1 80
return

:u_town
start_town
build 1 nexus 100
get_oldpeons 1
wait_buildstart 1 nexus
build 1 pylon 80
wait_build 1 pylon
build 1 probe 80
build 1 photon_cannon 80
wait_build 1 photon_cannon
build 1 gateway 80
build 8 probe 80
wait_buildstart 6 probe
build 12 probe 80
build 2 pylon 80
build 2 photon_cannon 80
:ue_loop
wait 2250

goto ue_loop

:horror_scarabs
wait 50

resources 13 AtMost Ore 100 horror_scarabs

issue_order 63 50 hreaver Loc.63 Loc.63 85 0
#scarab
goto horror_scarabs

:blue_support
notowns_jump gateway orange_hunters
multirun check_carrier
wait 3650

defensebuild_gg 2 zealot
defenseuse_gg 2 zealot
defensebuild_gg 1 dragoon
defenseuse_gg 1 dragoon
defensebuild_ag 1 dragoon
defenseuse_ag 1 dragoon
max_force 1400
define_max 10 zealot
define_max 10 dragoon
#dont replenish preplaced stuff in DT outpost
define_max 255 dark_templar
define_max 255 revenant
wait 5000

upgrade 1 p_armor 30
wait 5000

upgrade 1 dragoon_range 30
upgrade 1 p_plasma_shield 30
wait 5000

upgrade 1 p_ground_weapon 30
:blu_loop
wait 2250

goto blu_loop

:check_carrier
wait 5

deaths 13 Exactly 0 hcarrier check_carrier
deaths 13 Set 0 hcarrier check_carrier
train 1 carrier
wait 1000

goto check_carrier

:orange_hunters
defensebuild_aa 1 hcorsair
defenseuse_aa 1 hcorsair
max_force 400
define_max 2 hcorsair
define_max 1 hmojo
deaths 13 Set 0 wraith check_carrier
#use wraiths as markers to see what are our current limits
wait 450

multirun orange_check
multirun orange_cash
idle_orders 10 512 8 hmojo|hcorsair 2048 carrier 250 0
wait 5000

upgrade 1 p_plasma_shield 30
wait 4000

upgrade 1 p_air_weapon 40
defensebuild_gg 1 hmojo
defenseuse_gg 1 hmojo
defensebuild_ag 1 hcorsair
defenseuse_ag 1 hcorsair
max_force 600
wait 6000

define_max 3 hcorsair
define_max 2 hmojo
deaths 13 Set 1 wraith check_carrier
upgrade 1 scout_speed 30
upgrade 2 p_air_weapon 30
wait 5000

upgrade 2 p_plasma_shield 30
upgrade 1 p_plating 30
wait 7500

max_force 900
upgrade 3 p_air_weapon 30
upgrade 3 p_plasma_shield 30
wait 10500

define_max 4 hcorsair
deaths 13 Set 2 wraith check_carrier
upgrade 1 scout_sight 30
upgrade 2 p_plating 30
wait 5000

upgrade 3 p_plating 30
:oh_loop
wait 10050

train 3 hcorsair
goto oh_loop

:orange_cash
resources 13 Add Gas 200 orange_check
resources 13 Add Ore 300 orange_check
wait_rand 1667 2533

goto orange_cash

:orange_check
wait 50

#print orangeLooped
deaths 13 Exactly 1 wraith oc_limit_1
deaths 13 Exactly 2 wraith oc_limit_2
bring_jump 5 Exactly 0 carrier Loc.63 orange_check
#print ORANGE_GO
random_jump 85 oatk_1
random_jump 128 oatk_2
#print oatk_conti
wait_force 1 hmojo
wait_force 1 hcorsair
:o_loop_arnd
deaths 13 Set 1 dark_templar check_carrier
#set death. check in map to write value_area
wait_rand 3350 5000

goto orange_check

:oatk_1
#print oatk_1
wait_force 2 hcorsair
goto o_loop_arnd

:oatk_2
#print oatk_2
wait_force 1 hcorsair
wait_force 1 hmojo
goto o_loop_arnd

:oc_limit_1
wait 50

deaths 13 Exactly 2 wraith oc_limit_2
bring_jump 5 Exactly 0 carrier Loc.63 oc_limit_1
random_jump 85 oatk_1_l1
random_jump 128 oatk_2_l1
#print oatk_conti
wait_force 2 hcorsair
wait_force 1 hmojo
:o_loop_arnd_l1
deaths 13 Set 1 dark_templar check_carrier
#set death. check in map to write value_area
wait_rand 3350 5000

goto oc_limit_1

:oatk_1_l1
#print oatk_1
wait_force 2 hcorsair
goto o_loop_arnd_l1

:oatk_2_l1
#print oatk_2
wait_force 1 hcorsair
wait_force 2 hmojo
goto o_loop_arnd_l1

:oc_limit_2
wait 50

bring_jump 5 Exactly 0 carrier Loc.63 oc_limit_2
random_jump 85 oatk_1_l2
random_jump 128 oatk_2_l2
#print oatk_conti
wait_force 2 hcorsair
wait_force 1 hmojo
:o_loop_arnd_l2
deaths 13 Set 1 dark_templar check_carrier
#set death. check in map to write value_area
wait_rand 3350 5000

goto oc_limit_2

:oatk_1_l2
#print oatk_1
wait_force 3 hcorsair
goto o_loop_arnd_l2

:oatk_2_l2
#print oatk_2
wait_force 2 hcorsair
wait_force 2 hmojo
goto o_loop_arnd_l2